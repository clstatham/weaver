// Ignored
WHITESPACE    = _{ " " | "\t" | NEWLINE }
COMMENT       = _{ line_comment | block_comment }
line_comment  = _{ "//" ~ (!NEWLINE ~ ANY)* }
block_comment = _{ "/*" ~ (block_comment | (!"*/" ~ ANY))* ~ "*/" }

// Tokens
period   = _{ "." }
comma    = _{ "," }
colon    = _{ ":" }
semi     = _{ ";" }
lparen   = _{ "(" }
rparen   = _{ ")" }
lbrack   = _{ "[" }
rbrack   = _{ "]" }
lbrace   = _{ "{" }
rbrace   = _{ "}" }
plus     =  { "+" }
minus    =  { "-" }
star     =  { "*" }
slash    =  { "/" }
eq       =  { "=" }
plus_eq  =  { "+=" }
minus_eq =  { "-=" }
star_eq  =  { "*=" }
slash_eq =  { "/=" }

// Literals

letter         =  { ASCII_ALPHA | "_" }
string_literal = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
float_literal  = @{ ASCII_DIGIT+ ~ period ~ ASCII_DIGIT+ }
int_literal    = @{ ASCII_DIGIT+ }
literal        = _{ float_literal | int_literal | string_literal }
ident          = @{ (letter | "_") ~ (letter | ASCII_DIGIT | "_")* }

// Keywords
component = _{ "component" }
system    = _{ "system" }
query     = _{ "query" }
mutable   = _{ "mut" }
with      = _{ "with" }
without   = _{ "without" }

// Types
i32             = { "i32" }
i64             = { "i64" }
f32             = { "f32" }
f64             = { "f64" }
typ             = { i32 | i64 | f32 | f64 | ident }
typed_ident     = { ident ~ colon ~ typ }
mut_typed_ident = { mutable ~ ident ~ colon ~ typ }

// Top level
program    = { SOI ~ statements ~ EOI }
statements = { statement* }

// Statements
block          = { lbrace ~ statements ~ rbrace }
statement      = { (component_stmt | system_stmt | query_stmt | expr) ~ ";" }
component_stmt = { component ~ ident ~ lbrace ~ (typed_ident ~ semi)* ~ rbrace }
system_stmt    = { system ~ ident ~ block }
query_stmt     = { query ~ lbrack ~ typed_args ~ rbrack ~ with_clause? ~ without_clause? ~ block }
typed_args     = { ((mut_typed_ident | typed_ident) ~ comma)* ~ (mut_typed_ident | typed_ident)? }
with_clause    = { with ~ lbrack ~ (typ ~ comma)* ~ typ? ~ rbrack }
without_clause = { without ~ lbrack ~ (typ ~ comma)* ~ typ? ~ rbrack }

// Expressions

prefix       = _{ minus }
infix        = _{ plus | minus | star | slash }
call_expr    =  { ident ~ lparen ~ (expr ~ comma)* ~ expr? ~ rparen }
assign_expr  =  { ident ~ (plus_eq | minus_eq | star_eq | slash_eq | eq) ~ expr }
primary_expr = _{ literal | call_expr | assign_expr | ident | block | (lparen ~ expr ~ rparen) }
expr         =  { primary_expr ~ (infix ~ prefix? ~ primary_expr)* }
